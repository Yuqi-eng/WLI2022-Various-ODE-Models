# CellsAndCollagenLargerFull
# Capsular Contraction model for cells and collagen
# phi = macrophages
# F = fibroblasts
# M = myofibroblasts
# C = amount of collagen (aka tissue stiffness)

# cell transdifferentiation is dependent on C and phi, cell death rate is constant
# collagen production/stiffening is linear with cell density
# aphi is macrophage recruitment rate and is correlated with inflammation
# aphi=1 with all same other parameters has bistable system



dphi/dt = aphi - dphi*phi 
dF/dt = af + beta*phi - a3*Tr(phi)*F - a2*E(C)*F - df*F
dM/dt = am + a2*E(C)*F + a3*Tr(phi)*F - dm*M
dC/dt = a*M + b*F - dc*C

# am is the basal rate of myofibroblast influx 
#(transdifferentiation of fibroblasts into myofibroblasts)
# a2 is the additional stiffness-dependent influx
# C0 is the collagen (stiffness level) at which the cells
# are stimulated to transdifferentiate faster
# a is the collagen production (stiffening) caused by one cell
# dm and dc are the rates of turnover of the cells and the collagen

E(C)= C^n/(C0^n+C^n)
Tr(phi)=phi^mn/(phi0^mn+phi^mn)

# project onto F null surface and phi nullsurface:
#F= beta*phi/(a3*Tr(phi)+a2*E(C)+df)
#phi=aphi/dphi

param dm=1, dc=1, a=2, b=0.1, C0=2.5, n=6, am=0.1, a2=4
Param mn=4, phi0=1, a3=0.1, df=1, beta=1, af=1 
Param aphi=1, dphi=1

@ total=100,dt=0.01,xp=M,yp=C,xlo=0,xhi=3,ylo=0,yhi=5
done
